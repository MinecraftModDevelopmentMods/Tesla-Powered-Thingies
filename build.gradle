buildscript {
    ext.kotlin_version = project.kotlin_version
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "http://repo.maven.apache.org/maven2" }
        maven { url = "https://plugins.gradle.org/m2/" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.kotlin_version}"
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.10'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'kotlin'
apply plugin: "maven-publish"
apply plugin: "maven"
apply plugin: 'org.jetbrains.dokka'

archivesBaseName = project.mod_name.replace(' ', '-').toLowerCase()
group = "net.ndrei"

version = "${project.minecraft_version}-${project.mod_version}"
if (project.hasProperty('mod_suffix')) {
    version += "-${project.mod_suffix}"
}

compileJava { sourceCompatibility = targetCompatibility = "1.8" }
compileKotlin { kotlinOptions { jvmTarget = "1.8" } }

minecraft {
    version = "${project.minecraft_version}-${project.forge_version}"
    mappings = "${project.mcp_version}"
    runDir = "run"

    replace '@MOD-ID@', project.mod_id
    replace '@MOD-NAME@', project.mod_name
    replace '@MOD-VERSION@', project.mod_version
    replace '@MOD-MC-VERSION@', project.minecraft_version
    replace '@FORGELIN-VERSION@', project.forgelin_version
    replace '@FORGE-VERSION@', project.forge_version
    replace '@TESLA-CORE-LIB-VERSION@', project.teslacorelib_version
    replace '@FINGERPRINT@', project.findProperty('signSHA1')

    replaceIn 'constants.kt'
}

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ''
    }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file 'secret.json'
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

// Mad hacks to make source replacements work for Kotlin
// "stolen" from: https://github.com/PaleoCrafter/VanillaImmersion/blob/ee82ecafb76659cf7d7822a722c8f63f43f41d01/build.gradle#L119-L136
import net.minecraftforge.gradle.user.TaskSourceCopy
for (set in sourceSets) {
    def taskName = "source${set.name.capitalize()}Kotlin"
    def dir = new File(project.getBuildDir(), "sources/${set.name}/kotlin")
    task(taskName, type: TaskSourceCopy) {
        source = set.getKotlin()
        output = dir
    }
    def compileTask = tasks[set.getCompileTaskName("kotlin")]
    compileTask.source = dir
    compileTask.dependsOn taskName
    def dirPath = dir.toPath()
    compileTask.include {
        return it.file.toPath().startsWith(dirPath)
    }
}
// end of "stolen" part :)

configurations {
    compile.extendsFrom exportedCompile
    deployerJars

    all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { name='mmd'; url='https://maven.mcmoddev.com' }
    maven { name='forgelin'; url='http://maven.shadowfacts.net/' }

    maven { name='jei'; url="http://dvs1.progwml6.com/files/maven" }
    maven { name="ct2"; url="http://maven.blamejared.com" }
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${project.kotlin_version}"
    compile "net.shadowfacts:Forgelin:${project.forgelin_version}"

    if ((null == System.getenv("BUILD_TAG")) && file("Tesla-Core-Lib/build.gradle").exists()) {
        compile project(':Tesla-Core-Lib')
    }
    else {
        compile("net.ndrei:tesla-core-lib:${project.teslacorelib_mc_version}-${project.teslacorelib_version}:deobf") { changing = true }
    }

    if ((null == System.getenv("BUILD_TAG")) && file("Bush-Master-Core/build.gradle").exists()) {
        compile project(path: ':Bush-Master-Core')
    }
    else {
        compile("net.ndrei:bush-master-core:${project.minecraft_version}-${project.bushmaster_version}:deobf") { changing = true }
    }

    compileOnly "mezz.jei:jei_${project.minecraft_version}:${project.jei_version}:api"
    runtime "mezz.jei:jei_${project.minecraft_version}:${project.jei_version}"

    compileOnly "CraftTweaker2:CraftTweaker2-API:${project.ct_version}"
    runtime "CraftTweaker2:CraftTweaker2-MC1120-Main:${project.ct_mc_version}-${project.ct_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

jar {
    from sourceSets.api.output
    from sourceSets.main.output
}

task sourcesJar(type: Jar) {
    from sourceSets.api.allSource
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.api.output
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    classifier = 'api'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    kotlinTasks {
        [':compileApiKotlin']
    }
    outputDirectory = "$buildDir/dokkaJavadoc"
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    // Skips if the keyStore property is missing.
    onlyIf {
        project.hasProperty('keyStore')
    }

    // findProperty allows us to reference the property without it existing.
    // Using project.propName would cause the script to fail validation if
    // the property did not exist.
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives apiJar, sourceJar, deobfJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            def versionForMaven = "$project.minecraft_version-$project.mod_version"
            if (System.getenv().BUILD_NUMBER) {
                versionForMaven += '.' + System.getenv().BUILD_NUMBER
            }
            version versionForMaven
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            artifact deobfJar {
                classifier 'deobf'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
            artifact apiJar {
                classifier 'api'
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

curseforge {
    if (project.hasProperty('cf_release_type') && (project.cf_release_type != "none")) {
        //println("CURSE FORGE RELEASE :: $project.cf_release_type")
        apiKey = secret.curseforgeAPIKey
        project {
            id = "$project.cf_project_id"
            changelog = file('current-changelog.txt')
            releaseType = project.cf_release_type
            addGameVersion(project.minecraft_version)
            def projectName = project.mod_name
            def displayVersion = project.mod_version
            if (System.getenv().BUILD_NUMBER) {
                displayVersion += '.' + System.getenv().BUILD_NUMBER
            }
            mainArtifact(jar) {
                displayName = "$project.mod_name : $displayVersion"
                relations {
                    requiredLibrary 'shadowfacts-forgelin'
                    requiredLibrary 'tesla-core-lib'
                    optionalLibrary 'bush-master-core'
                }
            }
            addArtifact(sourceJar) {
                displayName = "$project.mod_name : $displayVersion : Sources"
            }
            addArtifact(deobfJar) {
                displayName = "$project.mod_name : $displayVersion : Development"
            }
            addArtifact(apiJar) {
                displayName = "$project.mod_name : $displayVersion : API"
            }
            addArtifact(javadocJar) {
                displayName = "$project.mod_name : $displayVersion : API Javadoc"
            }
        }
    }
}

if (System.getProperty("runClient.jvmArgs") != null) {
    runClient.jvmArgs System.getProperty("runClient.jvmArgs").split(" ")
}

if (System.getProperty("runServer.jvmArgs") != null) {
    runServer.jvmArgs System.getProperty("runServer.jvmArgs").split(" ")
}
